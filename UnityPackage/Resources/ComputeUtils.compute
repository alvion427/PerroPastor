#pragma multi_compile QUANT_SOURCE_32 QUANT_SOURCE_16 QUANT_SOURCE_Q8_0 QUANT_SOURCE_Q5_1
#pragma multi_compile QUANT_DEST_32 QUANT_DEST_16 QUANT_DEST_Q8_0
//#define QUANT_SOURCE_Q8_0 1

#include "Common.cginc"

#if QUANT_SOURCE_16
    #define SOURCE_BLOCK_TYPE uint2
    #define STORE_SOURCE StoreVec16f
    #define LOAD_SOURCE LoadVec16f
    #define SOURCE_IS_QUANTIZED 0
#elif QUANT_SOURCE_Q5_1
    #define SOURCE_BLOCK_VEC 8
    #define SOURCE_BLOCK_TYPE Q5_1_Block
    #define ENCODE_SOURCE Encode_Q5_1
    #define DECODE_SOURCE Decode_Q5_1
    #define SOURCE_IS_QUANTIZED 1
#elif QUANT_SOURCE_Q8_0
    #define SOURCE_BLOCK_VEC 8
    #define SOURCE_BLOCK_TYPE Q8_0_Block
    #define ENCODE_SOURCE Encode_Q8_0
    #define DECODE_SOURCE Decode_Q8_0
    #define SOURCE_IS_QUANTIZED 1
#else /* QUANT_SOURCE_32 */
    #define SOURCE_BLOCK_TYPE float4
    #define STORE_SOURCE StoreVec32f
    #define LOAD_SOURCE LoadVec32f
    #define SOURCE_IS_QUANTIZED 0
#endif

#if QUANT_DEST_16
    #define DEST_BLOCK_TYPE uint2
    #define STORE_DEST StoreVec16f
    #define LOAD_DEST LoadVec16f
    #define DEST_IS_QUANTIZED 0
#elif QUANT_DEST_Q8_0
    #define DEST_BLOCK_VEC 8
    #define DEST_BLOCK_TYPE Q8_0_Block
    #define ENCODE_DEST Encode_Q8_0
    #define DECODE_DEST Decode_Q8_0
    #define DEST_IS_QUANTIZED 1
#else /* QUANT_DEST_32 */
    #define DEST_BLOCK_TYPE float4
    #define STORE_DEST StoreVec32f
    #define LOAD_DEST LoadVec32f
    #define DEST_IS_QUANTIZED 0
#endif

#pragma kernel Quantize

StructuredBuffer<SOURCE_BLOCK_TYPE> quantize_input;
RWStructuredBuffer<DEST_BLOCK_TYPE> quantize_output;
uint quantize_numBlocks;
uint quantize_offset;

[numthreads(1024, 1, 1)]
void Quantize(uint3 id : SV_DispatchThreadID)
{
#if !SOURCE_IS_QUANTIZED
    if (id.x >= quantize_numBlocks)
        return;

#if DEST_IS_QUANTIZED
    uint destIndex = quantize_offset + id.x;
    DEST_BLOCK_TYPE block;
    int sourceIndex = destIndex * DEST_BLOCK_VEC;

    float scale = 0;

    // First compute scale for this block.
    int valueIndex;
    for (valueIndex = 0; valueIndex < DEST_BLOCK_VEC; ++valueIndex)
    {
        float4 source = LOAD_SOURCE(quantize_input[sourceIndex + valueIndex]);
        source = abs(source);
        scale = max(scale, max(max(source.x, source.y), max(source.z, source.w)));
    }

    scale /= 127.0f;
    block.scale = scale;

    // Now encode values with the given scale
    [unroll]
    for (valueIndex = 0; valueIndex < DEST_BLOCK_VEC; ++valueIndex)
    {
        float4 source = LOAD_SOURCE(quantize_input[sourceIndex + valueIndex]);
        ENCODE_DEST(source, block, valueIndex);
    }
    quantize_output[destIndex] = block;
#else
    uint index = id.x + quantize_offset;
    float4 source = LOAD_SOURCE(quantize_input[index]);
    DEST_BLOCK_TYPE dest = STORE_DEST(source);
    quantize_output[index] = dest;
#endif
#endif
}

#pragma kernel Dequantize

StructuredBuffer<SOURCE_BLOCK_TYPE> dequantize_input;
RWStructuredBuffer<DEST_BLOCK_TYPE> dequantize_output;
uint dequantize_numBlocks;
uint dequantize_offset;

[numthreads(1024, 1, 1)]
void Dequantize(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= dequantize_numBlocks)
        return;

#if SOURCE_IS_QUANTIZED
    uint sourceIndex = quantize_offset + id.x;
    const SOURCE_BLOCK_TYPE block = dequantize_input[sourceIndex];
    int destIndex = sourceIndex * SOURCE_BLOCK_VEC;

    [unroll]
    for (uint valueIndex = 0; valueIndex < SOURCE_BLOCK_VEC; ++valueIndex)
    {
        float4 v = DECODE_SOURCE(block, valueIndex);
        dequantize_output[destIndex + valueIndex] = STORE_DEST(v);
    }
#endif
}

#pragma kernel BlitToTexture

StructuredBuffer<SOURCE_BLOCK_TYPE> blit_input;
RWTexture2D<float> blit_output;
StructuredBuffer<int> blit_minmax;
uint blit_blockWidth;
uint blit_height;

[numthreads(32, 32, 1)]
void BlitToTexture(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= blit_blockWidth || id.y >= blit_height)
        return;

    float minVal = blit_minmax[0] / kFixedPointScale;
    float maxVal = blit_minmax[1] / kFixedPointScale;
    float range = maxVal - minVal;

#if !SOURCE_IS_QUANTIZED
    uint index = id.y * blit_blockWidth + id.x;
    float4 v = LOAD_SOURCE(blit_input[index]);

    for (int i = 0; i < 4; ++i)
    {
        float p = (v[i] - minVal) / range;
        uint x = id.x * 4 + i;
        blit_output[uint2(x, id.y)] = p;
    }

#else
    uint index = id.y * blit_blockWidth + id.x;
    const SOURCE_BLOCK_TYPE block = blit_input[index];

    for (uint valueIndex = 0; valueIndex < SOURCE_BLOCK_VEC; ++valueIndex)
    {
        float4 v = DECODE_SOURCE(block, valueIndex);

        for (int i = 0; i < 4; ++i)
        {
            float p = (v[i] - minVal) / range;
            uint x = id.x * SOURCE_BLOCK_VEC * 4 + valueIndex * 4 + i;
            blit_output[uint2(x, id.y)] = p;
        }
    }
#endif
}

#pragma kernel FindRange

StructuredBuffer<SOURCE_BLOCK_TYPE> findrange_input;
RWStructuredBuffer<int2> findrange_output;
uint findrange_blockCount;

[numthreads(1024, 1, 1)]
void FindRange(uint3 id : SV_DispatchThreadID)
{
    uint index = id.x;
    if (index >= findrange_blockCount)
        return;

    float minVal = 100000;
    float maxVal = -minVal;

#if !SOURCE_IS_QUANTIZED
    float4 v = LOAD_SOURCE(findrange_input[index]);
    minVal = min(minVal, min(min(v.x, v.y), min(v.z, v.w)));
    maxVal = max(maxVal, max(max(v.x, v.y), max(v.z, v.w)));

#else
    const SOURCE_BLOCK_TYPE block = findrange_input[index];

    for (uint valueIndex = 0; valueIndex < SOURCE_BLOCK_VEC; ++valueIndex)
    {
        float4 v = DECODE_SOURCE(block, valueIndex);
        minVal = min(minVal, min(min(v.x, v.y), min(v.z, v.w)));
        maxVal = max(maxVal, max(max(v.x, v.y), max(v.z, v.w)));
    }
#endif

    int minFixed = (int)(minVal * kFixedPointScale);
    int maxFixed = (int)(maxVal * kFixedPointScale);

    // TODO: Add groupshared min/max
    InterlockedMin(findrange_output[0].x, minFixed);
    InterlockedMax(findrange_output[0].y, maxFixed);
}
